plugins {
  id 'java'
  id 'maven-publish'
  id 'checkstyle'
  id 'java-library'
  id 'io.spring.dependency-management' version '1.1.5'
}

group 'uk.gov.hmcts.reform'

def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: "DEV-SNAPSHOT"
version buildNumber

checkstyle {
  toolVersion = '10.17.0'
  maxWarnings = 0
  getConfigDirectory().set(new File(rootDir, "config/checkstyle"))
}

sourceCompatibility = 17

repositories {
  mavenLocal()
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

jar {
  manifest {
    attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
  }
}

task printVersion {
  doLast {
    print project.version
  }
}

def pomConfig = {
  licenses {
    license {
      name "MIT License"
      url "http://www.opensource.org/licenses/mit-license.php"
      distribution "repo"
    }
  }

  scm {
    url "https://github.com/hmcts/cmc-pdf-service-client"
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  archiveClassifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

publishing {
  publications {
    Main(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      groupId project.group
      artifactId 'pdf-service-client'
      version project.version

      pom.withXml {
        def root = asNode()
        root.appendNode('description', 'Client library for communicating with cmc-pdf-service')
        root.appendNode('name', 'Platform PDF service client')
        root.appendNode('url', 'https://github.com/hmcts/cmc-pdf-service-client')
        root.children().last() + pomConfig
      }
    }
  }
}

test {
  useJUnitPlatform()
}

def springBootVersion = '3.3.0'

dependencyManagement {
  imports {
    mavenBom "org.springframework.boot:spring-boot-starter-parent:${springBootVersion}"
  }
}

dependencies {
  implementation group: 'org.springframework', name: 'spring-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'com.github.hmcts', name: 'java-logging', version: '6.1.5'
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.17.1'
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.10.2'
}
