plugins {
  id 'java'
  id 'maven'
  id 'maven-publish'
  id 'checkstyle'
  id 'java-library'
  id "com.jfrog.bintray" version "1.7.3"
}

group 'uk.gov.hmcts.reform'
version '3.0.0'

checkstyle.toolVersion = '7.2'
checkstyle.configFile = new File(rootDir, "checkstyle.xml")

// make build fail on Checkstyle issues (https://github.com/gradle/gradle/issues/881)
tasks.withType(Checkstyle).each { checkstyleTask ->
  checkstyleTask.doLast {
    reports.all { report ->
      def outputFile = report.destination
      if (outputFile.exists() && outputFile.text.contains("<error ")) {
        throw new GradleException("There were checkstyle warnings! For more info check $outputFile")
      }
    }
  }
}

sourceCompatibility = 1.8

repositories {
  mavenLocal()
  jcenter()
}

jar {
  manifest {
    attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
  }
}

task printVersion {
  doLast {
    print project.version
  }
}

def pomConfig = {
  licenses {
    license {
      name "MIT License"
      url "http://www.opensource.org/licenses/mit-license.php"
      distribution "repo"
    }
  }

  scm {
    url "https://github.com/hmcts/cmc-pdf-service-client"
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

publishing {
  publications {
    Main(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      groupId project.group
      artifactId 'pdf-service-client'
      version project.version

      pom.withXml {
        def root = asNode()
        root.appendNode('description', 'Client library for communicating with cmc-pdf-service')
        root.appendNode('name', 'Platform PDF service client')
        root.appendNode('url', 'https://github.com/hmcts/cmc-pdf-service-client')
        root.children().last() + pomConfig
      }
    }
  }
}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')
  publications = ['Main']
  publish = true
  pkg {
    repo = 'hmcts-maven'
    name = 'pdf-service-client'
    userOrg = 'hmcts'
    licenses = ['MIT']
    vcsUrl = 'https://github.com/hmcts/cmc-pdf-service-client'
    version {
      name = project.version
    }
  }
}

def springBootVersion = '1.5.6.RELEASE'

dependencies {
  compile group: 'org.springframework', name: 'spring-web', version: '4.3.10.RELEASE'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: springBootVersion
  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBootVersion
}
